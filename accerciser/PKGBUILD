# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=accerciser
pkgver=3.4.1
pkgrel=2
pkgdesc="Interactive Python accessibility explorer for GNOME"
arch=('any')
url="https://live.gnome.org/Accerciser"
license=('BSD')
depends=('python2-cairo' 'python2-gobject' 'pyatspi' 'at-spi2-atk' 'libwnck3' 'ipython2' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('intltool' 'gnome-doc-utils')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        Fix-for-bug-671261.patch)
sha256sums=('7f8deef3129060c5103f43bb94e29723747060c8feb5bb66a278ae48c44b3030'
            '6f36b6230abf56e621cdd49aabd4dd3ce1ecf186557e1169a54928daf9eb4e2c')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' src/accerciser.in plugins/ipython_view.py

  # https://bugzilla.gnome.org/show_bug.cgi?id=671261
  patch -Np1 -i "$srcdir/Fix-for-bug-671261.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-schemas-compile \
              PYTHON=python2
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
