# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: J. W. Birdsong <jwbirdsong AT gmail DOT com>
# Contributor: Luciano A. Ferrer <laferrer@gmail.com>

pkgname=byzanz
pkgver=0.3.0.1.git20120504
_pkgver=0.3.0.1
pkgrel=1
pkgdesc="Record what's happening on your desktop"
arch=('i686' 'x86_64')
url="http://git.gnome.org/browse/byzanz/"
license=('GPL')
depends=('gtk3' 'gstreamer0.10-base-plugins' 'hicolor-icon-theme' 'xdg-utils')
makedepends=('gnome-panel' 'intltool')
optdepends=('gnome-panel: panel applet')
install=$pkgname.install
source=(http://pkgbuild.com/~bgyorgy/sources/$pkgname-$pkgver.tar.xz)
md5sums=('0392165cafa8266ae9491ccf94a99ff8')

build() {
  cd "$srcdir/$pkgname-$_pkgver"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-static --disable-schemas-install \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas \
              CFLAGS="$CFLAGS -Wno-error"
  make
}

package() {
  cd "$srcdir/$pkgname-$_pkgver"

  make DESTDIR="$pkgdir/" install
}
