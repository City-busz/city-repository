# Maintainer: Balló György <ballogyor+arch at gmail dot com>
pkgname=devtools-city
pkgver=1
pkgrel=4
pkgdesc="Tools for György Balló package maintainer"
arch=('any')
url="http://pkgbuild.com/~bgyorgy/city.html"
license=('GPL')
depends=('devtools')
optdepends=('mono: for find-monodeps'
            'snakefood-hg: for find-pythondeps')
source=(find-monodeps.sh
        find-pythondeps.sh
        pacman-city.conf
        pacman-ayatana.conf
        citypkg.sh
        aurco.sh)
md5sums=('dba48cb20004259876d680a8ebaf6dc7'
         '34f935dc7c3577911970581e6d49a883'
         '189cf7eec19372a452789afadf6a33e0'
         '31a74880863ce044060859a1217c291b'
         '50d74bdb8f2a68c0f9a3aee87c18529e'
         'bccb46797dbc2c7a4994f0270330ee8d')

package() {
  cd "$srcdir"

  install -Dm755 "$srcdir/find-monodeps.sh" "$pkgdir/usr/bin/find-monodeps"
  install -Dm755 "$srcdir/find-pythondeps.sh" "$pkgdir/usr/bin/find-pythondeps"

  install -Dm644 pacman-city.conf "$pkgdir/usr/share/devtools/pacman-city.conf"
  ln -s archbuild "$pkgdir/usr/bin/city-i686-build"
  ln -s archbuild "$pkgdir/usr/bin/city-x86_64-build"

  install -Dm644 pacman-ayatana.conf "$pkgdir/usr/share/devtools/pacman-ayatana.conf"
  ln -s archbuild "$pkgdir/usr/bin/ayatana-i686-build"
  ln -s archbuild "$pkgdir/usr/bin/ayatana-x86_64-build"

  install -Dm755 citypkg.sh "$pkgdir/usr/bin/citypkg"
  ln -s citypkg "$pkgdir/usr/bin/ayatanapkg"
  install -Dm755 aurco.sh "$pkgdir/usr/bin/aurco"
}
