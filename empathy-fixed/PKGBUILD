# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Ionut Biru <ibiru@archlinux.org>

pkgname=empathy-fixed
_pkgname=empathy
pkgver=3.12.12+100+gb17da756d
pkgrel=1
pkgdesc="A GNOME instant messaging client using the Telepathy framework"
url="https://wiki.gnome.org/Apps/Empathy"
arch=(i686 x86_64)
license=(GPL2)
depends=(clutter-gst clutter-gtk folks gcr iso-codes libcanberra libpulse webkit2gtk
         telepathy-farstream telepathy-glib telepathy-logger telepathy-mission-control geoclue2
         geocode-glib cheese libchamplain libgudev)
makedepends=(intltool itstool docbook-xsl python gnome-common git)
optdepends=('telepathy-gabble: XMPP/Jabber support'
            'telepathy-idle: IRC support'
            'telepathy-salut: Link-local XMPP support'
            'telepathy-rakia: SIP support'
            'telepathy-haze: libpurple support')
conflicts=($_pkgname)
provides=($_pkgname)
_commit=b17da756dc9581544fd7ee4a134bd83440427a93  # master
source=("git+https://git.gnome.org/browse/empathy#commit=$_commit"
        "git+https://git.gnome.org/browse/telepathy-account-widgets"
        0001-Fix-crash-when-window-is-not-initialized.patch)
sha256sums=('SKIP'
            'SKIP'
            '3151c0260037e0bbc6d5667c0ff13f55e86ee8cf3c49f1f64e59e17e36875cf0')

pkgver() {
  cd $_pkgname
  git describe --tags | sed 's/^EMPATHY_//;s/_/./g;s/-/+/g'
}

prepare() {
  cd $_pkgname
  patch -Np1 -i ../0001-Fix-crash-when-window-is-not-initialized.patch

  git config --local submodule.telepathy-account-widgets.url "$srcdir/telepathy-account-widgets"
  sed -i 's/git submodule update/git submodule update --remote --merge/' ./autogen.sh
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd $_pkgname
  export LANG=en_US.UTF-8 LC_ALL=en_US.UTF-8
  ./configure --prefix=/usr \
      --sysconfdir=/etc  \
      --libexecdir=/usr/lib/empathy \
      --disable-Werror
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $_pkgname
  make DESTDIR="$pkgdir" install
}
