# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-devel-docs
pkgver=3.4.1
pkgrel=1
pkgdesc="Developer documentation (incl. HIG, Platform Guide, a11y Guide, etc.)"
arch=('any')
url="http://developer.gnome.org/"
license=('GPL')
depends=('yelp')
makedepends=('gnome-doc-utils')
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('5eb0cf7eb1fa232007c4b63e1283085e81e159eeefc379ec51ae2c4ea97a39dd')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' platform-demos/C/guitar-tuner/guitar-tuner.py platform-demos/C/image-viewer/image-viewer.py

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
