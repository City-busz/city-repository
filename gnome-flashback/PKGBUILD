# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-flashback
pkgver=3.14.0
pkgrel=1
pkgdesc="GNOME Flashback session"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Projects/GnomeFlashback"
license=('GPL')
depends=('gnome-panel' 'gnome-session' 'gnome-settings-daemon' 'metacity' 'polkit-gnome')
makedepends=('intltool' 'python2')
optdepends=('gnome-background: default background'
            'gnome-screensaver: lock srceen'
            'gnome-themes-standard: default theme'
            'notification-daemon: desktop notifications')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('5e3d344e86f7785fad188d97fb3dd5aa2bdffa41c9f9d94026534fa5e4739f8d')

prepare() {
  cd $pkgname-$pkgver

  # Add polkit-gnome to required components
  sed -i 's/^RequiredComponents=\(.*\)/RequiredComponents=\1polkit-gnome-authentication-agent-1;/' \
    data/gnome-flashback-*.session.in
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
