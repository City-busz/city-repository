# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-flashback
pkgver=3.16.1
pkgrel=1
pkgdesc="GNOME Flashback session"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Projects/GnomeFlashback"
license=('GPL')
depends=('gnome-panel' 'gnome-session' 'gnome-settings-daemon' 'metacity' 'polkit-gnome'
         'gjs' 'gnome-bluetooth')
makedepends=('autoconf-archive' 'intltool' 'python2')
optdepends=('gnome-background: default background'
            'gnome-screensaver: lock srceen'
            'gnome-screenshot: take screenshots'
            'network-manager-applet: network management'
            'notification-daemon: desktop notifications')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        git-fixes.patch
        0001-libscreenshot-Use-gnome-screenshot.patch
        0001-Add-autostart-file-for-nm-applet.patch
        {bluetooth,power}.js
        gnome-flashback-{bluetooth,power}-applet.desktop)
sha256sums=('b5424a28b2de694ae9bd170b73dfc6da62358c0e3527631b92be9dc400b52f90'
            '00fe405a2ea3a9fc50232361ea68ea95fdf6125d544482cae2dc2f80aad8e8a1'
            'd230935bc57facb25f161d317f72fc7896d49c0dcf5dbed39ef0f43e2b3ffb02'
            'a7911a273dd2ace9c2fe85fe03f8d7f9ae0e9f9658a9bdbbfa944aa4c3c40afb'
            '0c8a72f8baf564ac64972ebc463ac5c536fbb6afe3ce6e6a20fbeb0b528c3cfb'
            '5b79bedda8433b177ff07de70c054b2786efb685b9a9a4c6297abf5e52cbbd00'
            '7e89018b3cb893c3048839432c92049145a17c40a57681e4b2d514b96fd4945f'
            '11289a236a3fbe83248418908c4934ba708e1ed07d87d3264f638f56ab9f3c58')

prepare() {
  cd $pkgname-$pkgver

  # Apply fixes from git
  patch -Np1 -i ../git-fixes.patch

  # Use gnome-screenshot
  patch -Np1 -i ../0001-libscreenshot-Use-gnome-screenshot.patch

  # Add autostart file for nm-applet
  patch -Np1 -i ../0001-Add-autostart-file-for-nm-applet.patch

  # Add polkit-gnome to required components
  sed -i 's/^RequiredComponents=\(.*\)/RequiredComponents=\1polkit-gnome-authentication-agent-1;/' \
    data/gnome-flashback-*.session.in

  autoreconf -fi
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install

  # Add Bluetooth and power systray applets
  install -dm755 "$pkgdir"/usr/share/gnome-flashback
  install -m755 "$srcdir"/{bluetooth,power}.js "$pkgdir/usr/share/gnome-flashback"
  install -m644 "$srcdir"/gnome-flashback-{bluetooth,power}-applet.desktop "$pkgdir/etc/xdg/autostart"
}
