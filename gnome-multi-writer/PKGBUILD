# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-multi-writer
pkgver=3.22.1
pkgrel=1
pkgdesc="Write an ISO file to multiple USB devices at once"
arch=('x86_64' 'i686')
url="https://wiki.gnome.org/Apps/MultiWriter"
license=('GPL')
depends=('gtk3' 'libcanberra' 'libgusb' 'udisks2')
makedepends=('appstream-glib' 'docbook2x' 'intltool')
optdepends=('gnome-icon-theme-extras: show device icons')
source=("https://download.gnome.org/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz")
sha256sums=('02fe592b9c6c3b1b76dc7436f10a60673b8e89fb0f81f8e99ae943f6ba139fb6')

prepare() {
	cd $pkgname-$pkgver
	sed -i 's/docbook2man \$?/docbook2man --sgml \$?/' man/Makefile.{am,in}
}

build() {
	cd $pkgname-$pkgver
	./configure --prefix=/usr --libexecdir=/usr/lib/$pkgname
	make
}

package() {
	cd $pkgname-$pkgver
	make DESTDIR="$pkgdir" install
}
