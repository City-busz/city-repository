# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Jan de Groot <jgc@archlinux.org>

pkgname=gnome-panel
pkgver=3.16.1
pkgrel=1
pkgdesc="Panel of GNOME Flashback"
arch=('i686' 'x86_64')
url="https://wiki.gnome.org/Projects/GnomePanel"
license=('GPL')
depends=('gnome-desktop' 'gnome-menus' 'libwnck3' 'evolution-data-server' 'librsvg')
makedepends=('intltool' 'itstool' 'gobject-introspection')
install=$pkgname.install
source=(http://ftp.gnome.org/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz)
sha256sums=('b51997a5db70773606cd72be735902923ae73bd3f2c762ade65695751d4bd162')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var --libexecdir=/usr/lib/$pkgname \
              --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
