# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=gnome-weather
pkgver=0.1
pkgrel=1
pkgdesc="Access current weather conditions and forecasts"
arch=('any')
url="http://git.gnome.org/browse/gnome-weather/"
license=('GPL')
depends=('python-gobject' 'clutter-gtk' 'libgweather')
makedepends=('intltool')
source=(http://pkgbuild.com/~bgyorgy/sources/$pkgname-$pkgver.tar.xz
        backport-libgweather.patch)
sha256sums=('b8c960c116ad3284c6d115b6699cf1d2c77ae5ba42b9091804c7f2e7f26ed5e3'
            '17f09f40d445e0d66739c4d463dea5228e919db7581ac160b2ae9926d0b384af')

build() {
  cd "$srcdir/$pkgname-$pkgver"

  # Do not use libgweather features newer than version 3.6
  patch -Np1 -i "$srcdir/backport-libgweather.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static
  make
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
}
