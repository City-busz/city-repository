# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Ionut Biru <ibiru@archlinux.org>
# Contributor: Timm Preetz <timm@preetz.us>

pkgname=java-gnome
pkgver=4.1.1
pkgrel=2
pkgdesc="Java bindings to the GNOME platform (including gtk, glib and glade)"
arch=('i686' 'x86_64')
url="http://java-gnome.sourceforge.net"
license=('GPL')
depends=('java-runtime' 'gtksourceview3' 'librsvg' 'libunique3' 'libnotify' 'enchant')
makedepends=('java-environment' 'junit' 'python2')
source=(http://ftp.gnome.org/pub/gnome/sources/$pkgname/4.1/$pkgname-$pkgver.tar.xz
        fix-build.patch)
sha256sums=('b013020d3da99740bae17e60f29b94c1d999183128612eac1f19a4570b045db2'
            '4517eb34d9d7bf00806ffcefcae3e41eafca0fc08c3c7e3162668d09463fc761')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np0 -i "$srcdir/fix-build.patch"

  # python2 fix
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' build/faster src/util/demux/demux.py

  # adapt to the new build-system
  unset CLASSPATH
  if [ -d "$J2SDKDIR" ]; then
    ./configure jdk=$J2SDKDIR prefix=/usr
  else
    ./configure
  fi
  make
  DISPLAY= make doc
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make -j1 DESTDIR=$pkgdir install

  install -d "$pkgdir/usr/share/gtk-doc/html/$pkgname"
  cp -R "$srcdir"/$pkgname-$pkgver/doc/api/* "$pkgdir/usr/share/gtk-doc/html/$pkgname"
}
