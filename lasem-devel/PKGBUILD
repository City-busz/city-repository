# Maintainer: Balló György <ballogyor+arch at gmail dot com>

_pkgname=lasem
pkgname=$_pkgname-devel
pkgver=0.3.1
pkgrel=2
pkgdesc="SVG and Mathml rendering library (development release)"
arch=('i686' 'x86_64')
url="http://blogs.gnome.org/emmanuel/category/lasem/"
license=('LGPL')
depends=('gdk-pixbuf2' 'pango' 'libxml2')
makedepends=('intltool' 'gobject-introspection' 'gtk-doc')
optdepends=('lyx: ttf fonts for MathML')
provides=("$_pkgname=$pkgver")
conflicts=("$_pkgname")
options=('!libtool')
source=(http://ftp.gnome.org/pub/GNOME/sources/$_pkgname/${pkgver%.*}/$_pkgname-$pkgver.tar.bz2
        lasem-glib-2.32.patch)
sha256sums=('a0e8d6f958adb6af02b9d40875cec317c5504ddf64cb623975da72ffd224fa19'
            '46ed3156cea8e2cdf92fdf28d815677cbb1aa0ab9715cd93ee2b74002161741a')

build() {
  cd "$srcdir/$_pkgname-$pkgver"
  patch -Np1 -i "$srcdir/lasem-glib-2.32.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static
  make
}

package() {
  cd "$srcdir/$_pkgname-$pkgver"

  make DESTDIR="$pkgdir/" install
  rm -r "$pkgdir/usr/doc"
}
