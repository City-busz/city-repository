# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=longomatch
pkgver=0.16.9
pkgrel=1
pkgdesc="Video analysis software for sport analysts with unlimited possibilities"
arch=('i686' 'x86_64')
license=('GPL')
url="http://longomatch.org/"
depends=('gtk-sharp-2' 'db4o' 'gstreamer0.10-base-plugins' 'gnonlin' 'hicolor-icon-theme')
makedepends=('intltool')
optdepends=('gstreamer0.10-good-plugins: Extra media codecs'
            'gstreamer0.10-ugly-plugins: Extra media codecs'
            'gstreamer0.10-bad-plugins: Extra media codecs'
            'gstreamer0.10-ffmpeg: Extra media codecs')
options=('!libtool')
install=$pkgname.install
source=(http://ftp.acc.umu.se/pub/GNOME/sources/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz
        fix-gst-init.patch
        0001-Fix-CS1012-by-using-unicode-escape-sequences-instead.patch)
sha256sums=('2ba74c6add30b5d1625cc1208e637e8947f4ffde82c61b6182af4f340dc049c0'
            '42766b522e47dd36a5f9c7d13f726290996f2058325d307321b88aeddf7ebbea'
            '46b716770d539e9614a67c72e90da0f0471f6bc0e0fc03a4f47e5c63455fa97a')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/fix-gst-init.patch"
  patch -Np1 -i "$srcdir/0001-Fix-CS1012-by-using-unicode-escape-sequences-instead.patch"

  ./configure --prefix=/usr --sysconfdir=/etc --localstatedir=/var \
              --disable-static
  make -j1
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  make DESTDIR="$pkgdir" install
}
