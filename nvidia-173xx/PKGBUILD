# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>
# Contributor: Thomas Baechler <thomas@archlinux.org>

pkgname=nvidia-173xx
pkgver=173.14.36
_extramodules=extramodules-3.7-ARCH
pkgrel=7
pkgdesc="NVIDIA drivers for linux, 173xx branch."
arch=('i686' 'x86_64')
[ "$CARCH" = "i686"   ] && ARCH=x86
[ "$CARCH" = "x86_64" ] && ARCH=x86_64
url="http://www.nvidia.com/"
depends=('linux>=3.7' 'linux<3.8' "nvidia-173xx-utils=${pkgver}")
makedepends=('linux-headers>=3.7' 'linux-headers<3.8')
conflicts=('nvidia-96xx' 'nvidia')
license=('custom')
install=nvidia.install
source=("http://download.nvidia.com/XFree86/Linux-$ARCH/${pkgver}/NVIDIA-Linux-$ARCH-${pkgver}-pkg0.run"
        nvidia-kernel-3.7.patch)
options=(!strip)
md5sums=('513677824e9684dd34ca17ad56a68500'
         '4cddc386cdf2c22c81b12f9b7ffdb04b')
[ "$CARCH" = "x86_64" ] && md5sums=('c341612429c2885980103b40542cb2b2'
                                    '4cddc386cdf2c22c81b12f9b7ffdb04b')

build() {
        _kernver="$(cat /usr/lib/modules/${_extramodules}/version)"
	cd $srcdir
	sh NVIDIA-Linux-$ARCH-${pkgver}-pkg0.run --extract-only
	cd NVIDIA-Linux-$ARCH-${pkgver}-pkg0

	# Build fix from Gentoo: https://bugs.gentoo.org/show_bug.cgi?id=447566
	patch -Np0 -i "$srcdir/nvidia-kernel-3.7.patch"

	cd usr/src/nv/
	ln -s Makefile.kbuild Makefile
	make SYSSRC=/usr/lib/modules/${_kernver}/build module
}

package() {
	cd $srcdir/NVIDIA-Linux-$ARCH-${pkgver}-pkg0/usr/src/nv/
	mkdir -p $pkgdir/usr/lib/modules/${_extramodules}/
	install -m644 nvidia.ko $pkgdir/usr/lib/modules/${_extramodules}/

        mkdir -p $pkgdir/usr/lib/modprobe.d
        echo "blacklist nouveau" >> $pkgdir/usr/lib/modprobe.d/nouveau_blacklist.conf
	sed -i -e "s/EXTRAMODULES='.*'/EXTRAMODULES='${_extramodules}'/" $startdir/nvidia.install
        # gzip -9 module
        gzip "${pkgdir}/usr/lib/modules/${_extramodules}/nvidia.ko"
}
