# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=openbve
pkgver=1.4.2
_pkgver=1420
pkgrel=2
pkgdesc="Free-as-in-freedom train simulator placed in the public domain"
arch=('any')
url="http://odakyufan.zxq.net/openbve/"
license=('custom:public domain')
depends=('mono' 'taoframework' 'sdl' 'openal')
makedepends=('dos2unix')
source=(http://pkgbuild.com/~bgyorgy/sources/${pkgname}$_pkgver.7z
        openbve.sh
        openbve.desktop)
md5sums=('d282c34866b180a07a062dfbe389a90e'
         'fec92946f773f56588a71de310847ce0'
         'acb0dd3ea602910181b8cdb8375c794e')

build() {
  cd "$srcdir/SourceCode/openBVE"

  # Disable anti-aliasing by default (it's broken)
  sed -i 's/AntiAliasingLevel = 4/AntiAliasingLevel = 0/' OpenBve/OldCode/Interface.cs 

  xbuild /p:Configuration=Release OpenBve/OpenBve.csproj
}

package() {
  cd "$srcdir"

  # Binaries
  install -d "$pkgdir/usr/lib/$pkgname/"

  install -Dm644 SourceCode/openBVE/OpenBve/bin/Release/OpenBve.exe "$pkgdir/usr/lib/$pkgname/"
  install -Dm644 SourceCode/openBVE/OpenBve/bin/Release/OpenBveApi.dll "$pkgdir/usr/lib/$pkgname/"

  # Data
  mkdir -p "$pkgdir/usr/share/games/$pkgname"
  cp -r Data "$pkgdir/usr/share/games/$pkgname/"
  ln -s ../../share/games/$pkgname/Data "$pkgdir/usr/lib/$pkgname/Data"

  # Executable
  install -Dm755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"

  # Desktop file and icon
  install -Dm644 "$srcdir/$pkgname.desktop" $pkgdir/usr/share/applications/$pkgname.desktop
  mkdir -p "$pkgdir/usr/share/pixmaps"
  ln -s ../games/$pkgname/Data/Menu/logo.png "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # License
  install -Dm644 Credits.txt "$pkgdir/usr/share/licenses/$pkgname/Credits.txt"
}
