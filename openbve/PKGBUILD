# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=openbve
pkgver=1.4.1.2a
pkgrel=1
pkgdesc="Free-as-in-freedom train simulator placed in the public domain"
arch=('any')
url="http://odakyufan.zxq.net/openbve/"
license=('custom:public domain')
depends=('mono' 'taoframework' 'sdl' 'openal')
source=(http://pkgbuild.com/~bgyorgy/sources/$pkgname-$pkgver.tar.xz
        openbve.sh
        openbve.desktop)
md5sums=('6483b3a33872746ec3a16d202831541b'
         'fec92946f773f56588a71de310847ce0'
         'acb0dd3ea602910181b8cdb8375c794e')

build() {
  cd "$srcdir/$pkgname-$pkgver/Source/openBVE"
 
  xbuild /p:Configuration=Release OpenBve/OpenBve.csproj
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  # Binaries
  install -d "$pkgdir/usr/lib/$pkgname/"

  install -Dm644 Source/openBVE/OpenBve/bin/Release/OpenBve.exe "$pkgdir/usr/lib/$pkgname/"
  install -Dm644 Source/openBVE/OpenBve/bin/Release/OpenBveApi.dll "$pkgdir/usr/lib/$pkgname/"

  # Data
  mkdir -p "$pkgdir/usr/share/games/$pkgname"
  cp -r Data "$pkgdir/usr/share/games/$pkgname/"
  ln -s ../../share/games/$pkgname/Data "$pkgdir/usr/lib/$pkgname/Data"

  # Executable
  install -Dm755 "$srcdir/$pkgname.sh" "$pkgdir/usr/bin/$pkgname"

  # Desktop file and icon
  install -Dm644 "$srcdir/$pkgname.desktop" $pkgdir/usr/share/applications/$pkgname.desktop
  mkdir -p "$pkgdir/usr/share/pixmaps"
  ln -s ../games/$pkgname/Data/Menu/logo.png "$pkgdir/usr/share/pixmaps/$pkgname.png"

  # License
  install -Dm644 Credits.txt "$pkgdir/usr/share/licenses/$pkgname/Credits.txt"
  install -Dm644 Readme.txt "$pkgdir/usr/share/licenses/$pkgname/Readme.txt"
}
