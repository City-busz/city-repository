# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-browse
_pkgname=Browse
pkgver=157.3
pkgrel=1
pkgdesc="Sugar activity to browse the internet"
arch=('any')
url="http://wiki.sugarlabs.org/go/Activities/Browse"
license=('GPL')
groups=('sugar-fructose')
depends=('sugar' 'evince')
source=(http://download.sugarlabs.org/sources/sucrose/fructose/$_pkgname/$_pkgname-$pkgver.tar.bz2
        hu.patch)
sha256sums=('65c788d44ed3c77fc29e820e889ef345708a8611bcab59522183dc57d05c1b4c'
            'e36a20a883e8e1215bb30a3fee2f69e5e212013e479f09ebcc2397ec7aa66458')

prepare() {
  cd $_pkgname-$pkgver

  # Add missing parameter to Hungarian translation
  patch -Np1 -i ../hu.patch

  # Python2 fix
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' setup.py
}

package() {
  cd $_pkgname-$pkgver
  python2 setup.py install --prefix "$pkgdir/usr"
}
