# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-calculate
_pkgname=Calculate
pkgver=42
pkgrel=1
pkgdesc="Graphing calculator for Sugar"
arch=('any')
url="http://wiki.sugarlabs.org/go/Activities/Calculate"
license=('GPL')
groups=('sugar-activities')
depends=('sugar')
optdepends=('python2-matplotlib: alternative plotting backend')
source=(http://download.sugarlabs.org/sources/sucrose/fructose/$_pkgname/$_pkgname-$pkgver.tar.bz2
        gtk3.patch)
md5sums=('2248559d5c9c30d5469ed51972fdb60a'
         'c0633c8fba8bbeb993b169a4b3cd2882')

prepare() {
  cd $_pkgname-$pkgver

  # Python2 fix
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' setup.py

  # GTK+ 3 port
  patch -Np1 -i ../gtk3.patch
}

package() {
  cd $_pkgname-$pkgver
  python2 setup.py install --prefix "$pkgdir/usr"
}
