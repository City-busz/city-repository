# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-calculate
_pkgname=Calculate
pkgver=43
pkgrel=1
pkgdesc="Graphing calculator for Sugar"
arch=('any')
url="https://wiki.sugarlabs.org/go/Activities/Calculate"
license=('GPL')
groups=('sugar-fructose')
depends=('sugar-toolkit-gtk3')
optdepends=('python2-matplotlib: alternative plotting backend')
source=(https://download.sugarlabs.org/sources/sucrose/fructose/$_pkgname/$_pkgname-$pkgver.tar.bz2
        0001-Port-to-GTK3.patch)
sha256sums=('8fa75a00c5e2799708fdca8ee189c5596558c9c98c6cada3c2e4d6002911a5ed'
            '28fbbe30673dea2a69d1a6600ac35a56b0e3ffe81797b963f87d98364e1b27ab')

prepare() {
  cd $_pkgname-$pkgver
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' setup.py
  patch -Np1 -i ../0001-Port-to-GTK3.patch
}

package() {
  cd $_pkgname-$pkgver
  python2 setup.py install --prefix "$pkgdir/usr"
  rm -r "$pkgdir"/usr/share/applications
}
