# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-paint
_pkgname=Paint
pkgver=63
pkgrel=1
pkgdesc="Sugar activity to create paintings or drawings"
arch=('i686' 'x86_64')
url="http://wiki.sugarlabs.org/go/Activities/Paint"
license=('GPL')
depends=('sugar')
source=(http://download.sugarlabs.org/sources/honey/$_pkgname/$_pkgname-$pkgver.tar.bz2)
md5sums=('1652f8482a151a648a232dd1ccd0e5c1')

prepare() {
  cd $_pkgname-$pkgver

  # Python2 fix
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' setup.py
  sed -i 's/python-config/python2-config/' fill/Makefile

  # Remove prebuilt binaries
  rm -r fill/linux* fill/arm*
  sed -i 's/LIB_DIR =.*$/LIB_DIR = out/' fill/Makefile
}

build() {
  cd $_pkgname-$pkgver
  LD=gcc make -C fill
}

package() {
  cd $_pkgname-$pkgver
  python2 setup.py install --prefix "$pkgdir/usr"

  # Move binary module to libdir
  install -dm755 "$pkgdir/usr/lib/python2.7/site-packages/fill/"
  install -Dm644 fill/out/* "$pkgdir/usr/lib/python2.7/site-packages/fill/"
  rm -r "$pkgdir/usr/share/sugar/activities/Paint.activity/fill"
}
