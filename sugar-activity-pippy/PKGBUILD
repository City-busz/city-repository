# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-pippy
_pkgname=Pippy
pkgver=70
pkgrel=1
pkgdesc="Sugar activity to learn and develop in Python programming language"
arch=('any')
url="https://wiki.sugarlabs.org/go/Activities/Pippy"
license=('GPL')
groups=('sugar-fructose')
depends=('python2-pybox2d' 'python2-pygame' 'sugar' 'vte3')
source=($pkgname-$pkgver.tar.gz::https://github.com/sugarlabs/$_pkgname/archive/v$pkgver.tar.gz
        fix-missing-icons.patch)
sha256sums=('7c7ca995661a4e1c0964e5788ad5dfce539beca7acaa46b278b7f71429ccf575'
            '38bc051a8a4b5c2dbee2c1d4b73083eb8708b689935448b9767f5d339a684f44')

prepare() {
  cd $_pkgname-$pkgver
  patch -Np1 -i ../fix-missing-icons.patch
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'
  sed -i 's/python %s; sleep 1/python2 %s; sleep 1/' pippy_app.py
  sed -i "s/require_version('Vte', '2.90')/require_version('Vte', '2.91')/" activity.py pippy_app.py
  sed -i 's/self._collab.props.leader/self._collab._leader/' pippy_app.py texteditor.py
  rm -r library/box2d_*/ library/Box2D.py library/LICENSE
}

package() {
  cd $_pkgname-$pkgver
  python2 setup.py install --prefix "$pkgdir/usr"
  rm -r "$pkgdir"/usr/share/applications
}
