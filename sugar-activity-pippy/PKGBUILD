# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-pippy
_pkgname=Pippy
pkgver=70
pkgrel=1
pkgdesc="Sugar activity to learn and develop in Python programming language"
arch=('any')
url="https://wiki.sugarlabs.org/go/Activities/Pippy"
license=('GPL')
groups=('sugar-fructose')
depends=('python2-pygame' 'sugar-toolkit-gtk3' 'vte3')
source=($pkgname-$pkgver.tar.bz::https://github.com/sugarlabs/$_pkgname/archive/v$pkgver.tar.gz)
sha256sums=('7c7ca995661a4e1c0964e5788ad5dfce539beca7acaa46b278b7f71429ccf575')

prepare() {
  cd $_pkgname-$pkgver
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'
  sed -i 's/python %s; sleep 1/python2 %s; sleep 1/' pippy_app.py
}

package() {
  cd $_pkgname-$pkgver
  python2 setup.py install --prefix "$pkgdir/usr"
  rm -r "$pkgdir"/usr/share/applications
}
