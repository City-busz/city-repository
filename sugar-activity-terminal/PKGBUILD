# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-terminal
_pkgname=Terminal
pkgver=42
pkgrel=1
pkgdesc="Terminal emulator for Sugar"
arch=('any')
url="http://wiki.sugarlabs.org/go/Activities/Terminal"
license=('GPL')
groups=('sugar-activities')
depends=('sugar' 'vte3')
source=(http://download.sugarlabs.org/sources/sucrose/fructose/$_pkgname/$_pkgname-$pkgver.tar.bz2
        json.patch)
md5sums=('7b50f3521ec1c4e39a3a4fada984e19e'
         '9dbda6cec5456ec27616289d36da33fe')

prepare() {
  cd $_pkgname-$pkgver

  # Python2 fix
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' helpbutton.py setup.py

  # Use python's internal JSON library
  patch -Np1 -i ../json.patch
}

package() {
  cd $_pkgname-$pkgver
  python2 setup.py install --prefix "$pkgdir/usr"
}
