# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-activity-turtleblocks
_pkgname=TurtleBlocks
pkgver=209
pkgrel=1
pkgdesc="Logo-inspired graphical turtle for Sugar"
arch=('any')
url="http://wiki.sugarlabs.org/go/Activities/Turtle_Art"
license=('GPL')
groups=('sugar-fructose')
depends=('sugar' 'sugar-toolkit')
source=(http://download.sugarlabs.org/sources/sucrose/fructose/TurtleArt/$_pkgname-$pkgver.tar.bz2)
md5sums=('ec978e1101288ad2647fb37a9a0d320a')

prepare() {
  cd $_pkgname-$pkgver

  # Python2 fix
  find . -type f | xargs sed -i 's@^#!.*python$@#!/usr/bin/python2@'
}

package() {
  cd $_pkgname-$pkgver
  python2 setup.py install --prefix "$pkgdir/usr"

  # Fix permission
  chmod 644 "$pkgdir/usr/share/sugar/activities/TurtleBlocks.activity/samples/media-scratch.ta"
}
