# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-runner
pkgver=0.108.1
pkgrel=1
pkgdesc="Scripts to run Sugar"
arch=('i686' 'x86_64')
url="http://sugarlabs.org/"
license=('GPL')
depends=('sugar' 'xorg-xinit' 'xorg-server-xephyr' 'xorg-xrandr')
makedepends=('gobject-introspection')
install=$pkgname.install
source=(http://download.sugarlabs.org/sources/sucrose/glucose/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('282089180d024489fed7b59255b53ceddbfa517577e10d10746ddd8ebd4b3055')

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --libexecdir=/usr/lib
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
