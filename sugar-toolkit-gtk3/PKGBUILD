# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-toolkit-gtk3
pkgver=0.110.0
pkgrel=1
pkgdesc="Sugar GTK library"
arch=('i686' 'x86_64')
url="https://sugarlabs.org/"
license=('LGPL')
depends=('alsa-lib' 'python2-cairo' 'python2-dateutil' 'python2-dbus' 'python2-decorator'
         'python2-gobject' 'python2-telepathy' 'unzip' 'webkit2gtk')
makedepends=('intltool' 'gobject-introspection')
source=(https://download.sugarlabs.org/sources/sucrose/glucose/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('e4b626c4efb515a444eb7bc2d5897aca1affd710f328de95b426f9f13032373e')

prepare() {
  cd $pkgname-$pkgver
  sed -i -e '/GConf/d' -e '/client.set_int/d' src/sugar3/speech.py
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
