# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar-toolkit
pkgver=0.110.0
pkgrel=1
pkgdesc="GTK2 based toolkit for Sugar (deprecated)"
arch=('i686' 'x86_64')
url="https://sugarlabs.org/"
license=('LGPL')
depends=('hippo-canvas' 'python2-gconf' 'sugar-base' 'sugar-toolkit-gtk3')
makedepends=('intltool')
source=(https://download.sugarlabs.org/sources/sucrose/glucose/$pkgname/$pkgname-$pkgver.tar.bz2
        drop-rsvg-dependency.patch
        fix-presenceservice.patch)
sha256sums=('14e76fac034f8801c2d63c9b562e6559817a31aac72b1853370bf0bc66779f25'
            '80f28c29f43c1cb2ccb7107e05d5ce60b5f2160298262afeba3f98172e2fdb1a'
            '8da623f071be6f87b89dd6f1436c87ce2e8e5c63dd1c12c3db7081b701b715a8')

prepare() {
  cd $pkgname-$pkgver
  patch -Np1 -i ../drop-rsvg-dependency.patch
  patch -Np1 -i ../fix-presenceservice.patch
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr
  sed -i 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
