# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar
pkgver=0.102.0
pkgrel=1
pkgdesc="Sugar GTK shell"
arch=('any')
url="http://sugarlabs.org/"
license=('GPL')
depends=('gconf' 'gstreamer' 'gtksourceview3' 'libnm-glib' 'libwnck3' 'libxklavier'
         'python2-gwebsockets' 'sugar-datastore' 'sugar-toolkit-gtk3' 'webkitgtk')
makedepends=('intltool')
install=$pkgname.install
source=(http://download.sugarlabs.org/sources/sucrose/glucose/$pkgname/$pkgname-$pkgver.tar.xz)
md5sums=('540f08d29b2d894830c1d1b88f395cc4')

prepare() {
  cd $pkgname-$pkgver

  # Python2 fix
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' src/jarabe/model/update/aslo.py
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
              --disable-schemas-install --disable-schemas-compile
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  mv "$pkgdir/etc/gconf" "$pkgdir/usr/share"
}
