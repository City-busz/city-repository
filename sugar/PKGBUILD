# $Id$
# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=sugar
pkgver=0.108.1
pkgrel=1
pkgdesc="Sugar GTK shell"
arch=('any')
url="http://sugarlabs.org/"
license=('GPL')
depends=('gconf' 'gnome-keyring' 'gst-plugins-espeak' 'gtksourceview3' 'gvfs' 'libwnck3'
         'libxklavier' 'metacity' 'mobile-broadband-provider-info' 'networkmanager' 'openssh'
         'python2-gwebsockets' 'sugar-artwork' 'sugar-datastore' 'sugar-toolkit-gtk3'
         'telepathy-gabble' 'telepathy-mission-control' 'telepathy-salut' 'upower' 'webkitgtk'
         'xdg-user-dirs')
makedepends=('intltool')
install=$pkgname.install
source=(http://download.sugarlabs.org/sources/sucrose/glucose/$pkgname/$pkgname-$pkgver.tar.xz)
sha256sums=('f32eca4dd861a07266676050cb4215f550fed7d1245dc390492c6f80412e8a55')

prepare() {
  cd $pkgname-$pkgver

  # Python2 fix
  sed -i 's@^#!.*python$@#!/usr/bin/python2@' src/jarabe/model/update/aslo.py
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr --sysconfdir=/etc \
              --disable-schemas-install --disable-schemas-compile \
              --with-gconf-schema-file-dir=/usr/share/gconf/schemas
  make
}

package() {
  cd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
}
