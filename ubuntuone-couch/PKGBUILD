# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=ubuntuone-couch
pkgver=0.3.0
pkgrel=1
pkgdesc="Library and command line utilities to access a user's CouchDB data on the Ubuntu One servers"
arch=('any')
url="https://launchpad.net/ubuntuone-couch"
license=('GPL')
depends=('ubuntuone-client')
makedepends=('python-distutils-extra')
source=(http://launchpad.net/ubuntuone-couch/trunk/$pkgver/+download/$pkgname-$pkgver.tar.gz
        dynamic-timeout.patch
        fix-credentials.patch)
md5sums=('a30dedc90772c60ce59351254f1beacc'
         '22789633b90d590fd1ac103d140e5e71'
         '0cc9082fa41ffae57318c9588278f9c5')

build() {
  cd "$srcdir/$pkgname-$pkgver"
  patch -Np1 -i "$srcdir/dynamic-timeout.patch"
  patch -Np1 -i "$srcdir/fix-credentials.patch"

  python2 setup.py build
}

package() {
  cd "$srcdir/$pkgname-$pkgver"

  python2 setup.py install --root=$pkgdir/ --optimize=1
}
