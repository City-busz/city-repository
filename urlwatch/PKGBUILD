# Maintainer: Skydrome <skydrome@i2pmail.org>
# Contributor: Allan McRae <allan@archlinux.org>

pkgname=urlwatch
pkgver=1.16
pkgrel=1
pkgdesc="A tool for monitoring webpages for updates"
arch=('any')
url="http://thp.io/2008/urlwatch"
license=('BSD')
depends=('python2' 'python2-futures')
source=("http://thp.io/2008/urlwatch/$pkgname-$pkgver.tar.gz"
        '0001-handle-invalid-server-encoding.patch'
        '0002-fix-lynx-handing.patch')
md5sums=('f8cd952d6e531f534b4e22e3fb3665f1'
         '31a8fc763afbad78b5d1a45a3f66be0c'
         '3bd9a970eefee9ac0c412a41a2da455d')

prepare() {
    cd "$srcdir/$pkgname-$pkgver"
    patch -Np1 -i "$srcdir/0001-handle-invalid-server-encoding.patch"
    patch -Np1 -i "$srcdir/0002-fix-lynx-handing.patch"
}

build() {
    cd "$srcdir/$pkgname-$pkgver"
    python2 setup.py build
}

package() {
    cd "$srcdir/$pkgname-$pkgver"
    python2 setup.py install --root="$pkgdir"
    install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}
